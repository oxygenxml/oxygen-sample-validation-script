task initValidation() {
    doFirst {
        if (!new File("${projectDir}/extracted/oxygen/").exists()) {
            ant.get(src: 'https://www.oxygenxml.com/InstData/Editor/All/oxygen.tar.gz', dest: "${projectDir}/oxygen.tar.gz", skipexisting: 'true')
            ant.untar(src: "${projectDir}/oxygen.tar.gz", dest: "${projectDir}/extracted", compression: 'gzip')

            String key = System.getenv('SCRIPTING_LICENSE_KEY')
            if (key == null) {
                throw new TaskExecutionException(it, new Exception("You need a valid Oxygen Scripting license key."))
            } else {
                new File("${projectDir}/extracted/oxygen/scriptinglicensekey.txt").write(key.replaceAll("  ", "\n"))
            }
        }
    }
    doLast {
        project.exec {
            workingDir "${projectDir}/extracted/oxygen/scripts"
            commandLine 'chmod', '700', 'validate.sh'
        }
    }
}

task runValidation(type: Exec, dependsOn: [initValidation]) {
    // Compute the validation directory depending on where the script is started from.
    String valDir = "${projectDir}" + (localBuild.toBoolean() ? "/../validation/" : "/../../") + validationDir
    workingDir "${projectDir}/extracted/oxygen"
    commandLine "${projectDir}/extracted/oxygen/scripts/validate.sh",
            valDir,
            "-sf", "${projectDir}/../validateScript.xpr", 
            "-rft", "html", 
            "-rf", "${projectDir}/../report/validationReport.html"

    doLast {
        if (execResult.getExitValue() != 0) {
            println new File("${projectDir}/../report/validationReport.html").text
            throw new TaskExecutionException(it, new Exception("Validation failed, see task output for details."))
        } else {
            // Replace file locations from GitHub VM with locations from repository.
            ant.replace(
                    file: '../report/validationReport.html',
                    token: 'file:/home/runner/work/oxygen-sample-validation-script/oxygen-sample-validation-script',
                    value:  repoMainBranchUrl
            )
            ant.replace(
                    file: '../report/validationReport.html',
                    token: '/home/runner/work',
                    value:  ''
            )
        }
    }
}

defaultTasks 'runValidation'

